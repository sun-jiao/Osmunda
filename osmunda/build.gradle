apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

group='moe.sunjiao'

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 5
        versionName "1.5.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'

    implementation 'org.ogce:xpp3:1.1.6'

    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-cli:commons-cli:1.3'
    implementation 'org.apache.commons:commons-lang3:3.6'

    implementation 'org.apache.commons:commons-compress:1.18'
    testImplementation 'junit:junit:4.13'

    api 'moe.sunjiao.osmosis:osmosis-core:0.48.2'
    api 'moe.sunjiao.osmosis:osmosis-pbf:0.48.2'
    api 'moe.sunjiao.osmosis:osmosis-osm-binary:0.48.2'
    api 'moe.sunjiao.osmosis:osmosis-xml:0.48.2'

    implementation 'org.osmdroid:osmdroid-android:6.1.6'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}